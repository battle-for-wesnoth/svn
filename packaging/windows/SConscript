# vi: syntax=python:et:ts=4
from os.path import join
from glob import glob
Import("env")

binaries = ["wesnoth", "wesnoth_editor"]
for bin in binaries:
    #icon = env.Command(bin + "-icon.ico", join("../../icons", bin + "-icon.png"), "$IM_CONVERT $SOURCE $TARGET")
    try:
        res = [env.RES(bin + ".rc")]
        #env.Depends(res, icon)
    except AttributeError:
        res = []
    locals()[bin + "_res"] = res
    Export(bin + "_res")

if "windows-release" in COMMAND_LINE_TARGETS:
    env.Tool("scanreplace", toolpath = ["../../scons"])
    env["packaging_dir"] = Dir(".").abspath
    iss = env.ScanReplace(join(env["destdir"], "Wesnoth.iss"), "Wesnoth.iss.in")
    Depends(iss, Value(env.Dictionary()))
    Alias("windows-release", iss)
    
    docs = [ File(doc, "../..") for doc in Split("README COPYING COPYRIGHT") ]
    datadirs = [ Dir(dir, "../..") for dir in Split("data fonts images sounds translations") ]
    dlls = glob("../../*.dll")
    Alias("windows-release", env.InstallFiltered(Dir(env["destdir"]), datadirs))
    Alias("windows-release", env.Install(env["destdir"], docs + dlls))
