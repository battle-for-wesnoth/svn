/**
 * @author Timotei Dolean
 */
 //grammar org.wesnoth.WML with org.eclipse.xtext.common.Terminals
grammar org.wesnoth.WML hidden(WS, SL_COMMENT)
generate wML "http://www.wesnoth.org/WML"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
WMLRoot:
	( tags+=WMLTag
	| macroCalls+=WMLAbstractMacroCall
	)*
;

WMLTag:
	'[' (plus?='+')? name=ID ']'
		( tags+=WMLTag
		| keys+=WMLKey
		| macroCalls+=WMLAbstractMacroCall
		)*
	'[/' endName=ID ']'
;

WMLKey:
	name=ID '=' value=WMLAbstractKeyValue ('+' extraArgs+=WMLAbstractKeyValue)*
;

WMLAbstractMacroCall:
	WMLMacroInclude | WMLMacroCall
;

WMLMacroInclude:
	'{' '~' path = WMLKeyValue '}'
;
WMLMacroCall:
	'{' name=ID 
		(
			( params+=WMLAbstractKeyValue 
			)*		
		)?
	'}'
;

WMLAbstractKeyValue:
	WMLKeyValue | WMLMacroCall
;
WMLKeyValue:
	(WMLANYVALUE)+
;
WMLANYVALUE:
	ID | INT | STRING | TSTRING | ANY_OTHER;
TSTRING:
	'_' STRING
;

terminal ID  		: ('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'-')*;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING	: '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"';
terminal SL_COMMENT 	: '#' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t')+;
terminal EOL			: ('\r'|'\n')+;
terminal ANY_OTHER: .;