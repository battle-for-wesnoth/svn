/**
 * @author Timotei Dolean
 */
 //grammar org.wesnoth.WML with org.eclipse.xtext.common.Terminals
grammar org.wesnoth.WML hidden(WS, SL_COMMENT)
generate wML "http://www.wesnoth.org/WML"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

//TODO: add cross-reference for variables
//handles TODO:
// - arrays [ ]

WMLRoot:
	( Tags += WMLTag
	| Macros += WMLMacro
	)*
	;

WMLTag:
	'[' (plus?='+')? name=ID ']'
		( Tags += WMLTag
		| Keys += WMLKey
		| Macros += WMLMacro // this generates multiple alternatives
		)*
	'[/' endName = ID ']'
	;

WMLKey:
	(name = ID) '='
	value += WMLKeyValueRule+;

WMLKeyValueRule:
	WMLKeyValue | WMLMacro | WMLLuaCode;

WMLKeyValue:
	value = (ID | STRING | ANY_OTHER | '+');
	
WMLMacro:
	name = MACRO;
WMLLuaCode:
	value = LUA_CODE;

terminal LUA_CODE   : '<<' -> '>>';
terminal MACRO      : '{' -> '}';
terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;
terminal ID         : ('a'..'z'|'A'..'Z'|'0'..'9'|'_'|',')+;
terminal STRING     : '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"';
terminal WS         : (' '|'\t'|'\r'|'\n')+;
terminal ANY_OTHER  : .;
